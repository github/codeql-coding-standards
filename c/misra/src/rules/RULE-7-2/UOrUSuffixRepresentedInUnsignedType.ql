/**
 * @id c/misra/u-or-u-suffix-represented-in-unsigned-type
 * @name RULE-7-2: A 'U' or 'u' suffix shall be applied to all unsigned integers constants
 * @description A  'U' or 'u' suffix shall be applied to all integer constants that are represented
 *              in an unsigned type.
 * @kind problem
 * @precision very-high
 * @problem.severity recommendation
 * @tags external/misra/id/rule-7-2
 *       maintainability
 *       readability
 *       external/misra/obligation/required
 */

import cpp
import codingstandards.c.misra

from Literal l
where
  not isExcluded(l, SyntaxPackage::uOrUSuffixRepresentedInUnsignedTypeQuery()) and
  not l instanceof StringLiteral and
  // Determine if the extractor deduced that the literal is unsigned, based on the C rules
  l.getType().(IntegralType).isUnsigned() and
  // And report if the literal does not contain a 'U' or 'u' suffix, e.g. explicitly unsigned
  not exists(l.getValueText().toUpperCase().indexOf("U")) and
  // Exclude constants generated by macro expansions, because the suffix information is lost in this
  // case, so can cause false positives.
  not l.isInMacroExpansion()
select l,
  "Unsigned literal " + l.getValueText() +
    " does not explicitly express sign with a 'U' or 'u' suffix."
